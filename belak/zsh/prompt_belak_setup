#
# A simple theme that displays relevant, contextual information, based on the
# sorin theme from prezto with as much unicode removed as possible.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Kaleb Elwert <belak@coded.io>
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_belak_pwd {
  #local pwd="${PWD/#$HOME/~}"

  #if [[ "$pwd" == (#m)[/~] ]]; then
  #  _prompt_belak_pwd="$MATCH"
  #  unset MATCH
  #else
  #  _prompt_belak_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  #fi

  _prompt_belak_pwd=$(promptpwd)
}

function prompt_belak_venv {
  _prompt_belak_venv=""
  if [[ -n $VIRTUAL_ENV ]]; then
    _prompt_belak_venv=" [${VIRTUAL_ENV##*/}]"
  fi
}

function prompt_belak_git_info {
  if (( _prompt_belak_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_belak_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_belak_precmd_async_data"
      RPROMPT+='${git_info:+${(e)git_info[status]}}'
      unalias typeset
    fi

    # Reset PID.
    _prompt_belak_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_belak_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_belak_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_belak_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_belak_pwd

  # Format Virtual Env
  prompt_belak_venv

  # Define prompts.
  RPROMPT='${editor_info[overwrite]}${VIM:+" %B%F{6}V%f%b"}'

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_belak_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_belak_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_belak_git_info WINCH
  prompt_belak_precmd_async &!
  _prompt_belak_precmd_async_pid=$!
}

function prompt_belak_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_belak_precmd_async_pid=0
  _prompt_belak_precmd_async_data="${TMPPREFIX}-prompt_belak_data"

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_belak_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'

  #zstyle ':prezto:module:git:info:stashed' format '%%B%F{6}x%f%%b'
  zstyle ':prezto:module:git:info:added' format '%%B%F{2}*%f%%b'
  zstyle ':prezto:module:git:info:modified' format '%%B%F{3}x%f%%b'

  # Deleted, renamed, created
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{1}x%f%%b'
  #zstyle ':prezto:module:git:info:renamed' format '%%B%F{5}*%f%%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{7}x%f%%b'

  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{3}unmerged%f%%b'

  # State modifier
  #zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:action' format '%%B%F{9}%s%f%%b%F{7}:%f'

  # This is the name of what we're looking at.
  zstyle ':prezto:module:git:info:branch' format '%%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format '%%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:position' format '%%B%F{13}%p%f%%b'

  # How far ahead or behind we are
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{13}+%A%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{3}-%B%f%%b'

  # This is overall display
  zstyle ':prezto:module:git:info:keys' format \
    'status' ' %S%a%m%r%d%u%U [%s$(coalesce "%b" "%p" "%c")%A%B]'
  #'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{10}%n@%m%f "}%F{10}${_prompt_belak_pwd}%f${_prompt_belak_venv}%(!. %B%F{1}#%f%b.) %(?:%F{12}:%F{9})%#%f '
  RPROMPT=''
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_belak_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'belak'
}

prompt_belak_setup "$@"
